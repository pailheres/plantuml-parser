start: "@startuml" (skinparam | class_def | relationship)* "@enduml"

// Skinparam
skinparam: "skinparam" sparam svalue

// Classes
class_def: "class" CLASS_NAME ("{" vari_or_func* "}")?

// Attribute Scope
var_scope: "{static}" -> static
         | "{abstract}" -> abstract

// Variables and Methods visibility
_visibility: VISIBILITY

// Variables or Functions
?vari_or_func: var_scope? _visibility variable
             | _visibility function
               
// Variables
variable: VAR_NAME ":" TYPE

// Functions
function: FUNC_NAME "(" param_list ")" ":" TYPE
param_list: (param ("," param)*)?
param: PARAM_NAME ":" TYPE

// Relationships
relationship: arrow_end_class_name_1 cardinality? arrow_head_1? arrow_body arrow_head_2? cardinality? arrow_end_class_name_2 (":" relation_text)?

// Arrow Heads
!arrow_head_1: "<"
             | "<|"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_head_2: ">"
             | "|>"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_body: ("-"
           | "--"
           | "---"
           | "-left-"
           | "-up-"
           | "-right-"
           | "-down-"
           | "."
           | ".."
           | ".left."
           | ".up."
           | ".right."
           | ".down." )

cardinality: ESCAPED_STRING

//RULES
sparam: CNAME
svalue: CNAME|WORD|ESCAPED_STRING|DIGIT
arrow_end_class_name_1: CNAME
arrow_end_class_name_2: CNAME
relation_text: (CNAME|WORD|ESCAPED_STRING|DIGIT|">"|"<")?
relation_class_name_1: CNAME
relation_class_name_2: CNAME

//TERMINALS
CLASS_NAME: CNAME
TYPE: (CNAME)("["|"]"|"*")*
VAR_NAME: CNAME
FUNC_NAME: CNAME
PARAM_NAME: CNAME
VISIBILITY: ("+"  //-> public
           | "-"  //-> private
           | "#"  //-> protected
           | "~") //-> package


%import common.CNAME
%import common.ESCAPED_STRING
%import common.WORD
%import common.DIGIT
%import common.LETTER
%import common.WS
%ignore WS
